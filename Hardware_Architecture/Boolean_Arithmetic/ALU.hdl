/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // X
        Mux16(a=x , b=false , sel=zx , out=zxOut );
        Not16(in=zxOut , out=NotzxOut );
        Mux16(a=zxOut , b=NotzxOut , sel=nx , out=nxOut );

        // Y
        Mux16(a=y , b=false , sel=zy , out=zyOut );
        Not16(in=zyOut , out=NotzyOut );
        Mux16(a=zyOut , b=NotzyOut , sel=ny , out=nyOut );

        // F
        And16(a=nxOut , b=nyOut , out=fAnd );
        Add16(a=nxOut , b=nyOut , out=fAdd );
        Mux16(a=fAnd , b=fAdd , sel=f , out=fOut );

        // Out
        Not16(in=fOut , out=NotfOut );
        Mux16(a=fOut , b=NotfOut , sel=no , out=out , out[15]=ng );

        // Zr
        Mux16(a=fOut , b=NotfOut , sel=no , out[0..7]=zr1 , out[8..15]=zr2 );
        Or8Way(in=zr1 , out=zr3 );
        Or8Way(in=zr2 , out=zr4 );
        Or(a=zr3 , b=zr4 , out=zr5 );
        Not(in=zr5 , out=zr );
}
